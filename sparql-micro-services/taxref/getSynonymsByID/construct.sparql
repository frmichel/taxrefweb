CONSTRUCT {
    []
        dwc:scientificName              ?full_name;
        dwc:scientificNameAuthorship    ?authorname;
        dwc:namePublishedInYear         ?year;
        schema:sameAs                   ?webpage;
        .
}
WHERE {
    ?content
        api:id              ?id;
        api:scientificName  ?scientific_name;
        api:authority       ?author;
        api:_links          [ api:inpnWebpage [ api:href ?url ] ];
        .
        
    bind(sms:getYearFromAuthor(?author) AS ?year)
    bind(sms:formatAuthorName(?author) AS ?authorname)
    bind(concat(?scientific_name, " ", ?authorname) AS ?full_name)
    bind(IRI(?url) AS ?webpage)
}

#Return the year in which a name was coined, given the full author name
function sms:getYearFromAuthor(?author){
    replace(?author, "^.*([1-2][0-9]{3}).*$", "$1")
}

# Format author name removing extra spaces at beginning and end of the name and after a dot
# " (Bloch, 1782)" => "(Bloch, 1782)";
# "A. Gray" => "A.Gray"
function sms:formatAuthorName(?author){
    replace(replace(?author, "(\\. )", "."), "(^ *)|( *$)", "")
}

