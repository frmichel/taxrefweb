CONSTRUCT {
    ?taxonUri 
        a                   dwc:Taxon;
        dwc:taxonID         ?id;
        dwc:scientificName  ?scientificname;
        taxrefp:hasRank     ?taxrefRankUri;
        schema:sameAs       ?url;
        skos:broader        ?parentTaxon;
        .
}
WHERE {
    ?c
        api:id              ?id;
        api:scientificName  ?scientificname;
        api:rankId          ?rankID;
        api:parentId        ?parentID;
        api:_links [
            api:inpnWebpage [
                api:href    ?page
             ]
        ]

    bind(IRI(concat("http://example.org/sparql-ms/taxref/getRankByRankID?id=", ?rankID)) AS ?service)
    SERVICE ?service {
        [] schema:url ?taxrefRankUri.
    }

    bind(iri(?page) AS ?url)
    bind(IRI(concat("http://example.org/ld/taxref/taxon/", ?id)) AS ?taxonUri)
    bind(IRI(concat("http://example.org/ld/taxref/taxon/", ?parentID)) AS ?parentTaxon)
}
