PREFIX taxrefp: <http://taxref.mnhn.fr/lod/property/>.

CONSTRUCT {
    ?taxonUri a dwc:Taxon;
        
        dwc:acceptedNameUsage        ?acceptedName;
        dwc:acceptedNameUsageID      ?acceptedNameId;
        dwc:scientificName           ?canonicalName;
        dwc:scientificNameID         {id};        # Reinject the service argument = ?key
        dwc:scientificNameAuthorship ?authorshipname;
        dwc:namePublishedIn          ?publishedIn;
        dwc:nameAccordingTo          ?accordingTo;
        dwc:taxonomicStatus          ?status;

        dwc:kingdom                  ?kingdom;
        dwc:phylum                   ?phylum;
        dwc:class                    ?class;
        dwc:order                    ?order;
        dwc:family                   ?family;
        dwc:genus                    ?genus;

        taxrefp:hasRank              ?rankUri;
        dwc:taxonRank                ?rankName;
        schema:sameAs                ?page;
        skos:broader                 ?parentUri;
		.
}
WHERE {
    ?content
        api:key             ?key;
        api:authorship      ?authorship;
	    api:canonicalName   ?canonicalName;
        api:scientificName  ?scientificName.
        
    OPTIONAL { ?content api:accepted        ?accepted }
    OPTIONAL { ?content api:acceptedKey     ?acceptedKey }
    OPTIONAL { ?content api:taxonomicStatus ?status }
    OPTIONAL { ?content api:kingdom         ?kingdom }
    OPTIONAL { ?content api:phylum          ?phylum }
    OPTIONAL { ?content api:order           ?order }
    OPTIONAL { ?content api:family          ?family }
    OPTIONAL { ?content api:genus           ?genus}
    OPTIONAL { ?content api:rank            ?rankName }
    OPTIONAL { ?content api:class           ?class }
    OPTIONAL { ?content api:parentKey       ?parentKey }
    OPTIONAL { ?content api:publishedIn     ?publishedIn }
    OPTIONAL { ?content api:accordingTo     ?accordingTo }
  
    bind (IF(?status = "ACCEPTED", ?scientificName, ?accepted) AS ?acceptedName)
    bind (str(IF(?status = "ACCEPTED", ?key, ?acceptedKey)) AS ?acceptedNameId)
    
    bind (IRI(concat("http://example.org/ld/gbif/id/", ?key)) AS ?taxonUri)
    bind (IRI(concat("https://www.gbif.org/species/", ?key)) AS ?page)

    bind (IRI(concat("http://example.org/ld/gbif/id/", ?parentKey)) AS ?parentUri)

    bind (sms:formatAuthorName(?authorship) AS ?authorshipname)
    
    bind (IRI(concat("http://taxref.mnhn.fr/lod/taxrank/", sms:getTaxrefRankName(?rankName))) AS ?rankUri)	
}

# Translate the GBIF rank name into the corresponding TAXREF-LD name.
# The ranks in GBIF that do not exist in TAXREF-LD will still be translated into URIs 
# following the Camel-case convention. This applies to: 
# SUPERLEGION, SUBLEGION, INFRALEGION, SUPERCOHORT, SUBCOHORT, INFRACOHORT, MAGNORDER, GRANDORDER, 
# INFRAFAMILY, INFRATRIBE, SUPRAGENERIC_NAME, INFRAGENUS, INFRAGENERIC_NAME, INFRASPECIFIC_NAME,
# CONVARIETY, INFRASUBSPECIFIC_NAME, PROLES, GREX, PATHOVAR, BIOVAR, CHEMOVAR, MORPHOVAR, PHAGOVAR,
# SEROVAR, CHEMOFORM, FORMA_SPECIALIS, CULTIVAR_GROUP, STRAIN, OTHER, UNRANKED
function sms:getTaxrefRankName(?rankName) {
    if      (?rankName = "LEGION")              { return("Legio") }
    else if (?rankName = "SPECIES_AGGREGATE")   { return("Aggregate") }
    else if (?rankName = "ABERRATION")          { return("Abberatio") }
    else if (?rankName = "MORPH")               { return("Morpha") }
    else if (?rankName = "FORM")                { return("Forma") }
    else if (?rankName = "SUBFORM")             { return("SubForma") }
    else if (?rankName = "VARIETY")             { return("Varietas") }
    else if (?rankName = "SUBVARIETY")          { return("SubVarietas") }

    # Transform "SUBABCD..." into "SubAbc", then same thing for Super, Infra and Parv
    else if (strstarts(?rankName, "SUB")) {
        let (?after = strafter(?rankName, "SUB")) {
            return(concat("Sub", substr(?after, 1, 1), lcase(substr(?after, 2))))
        }
    }
    else if (strstarts(?rankName, "SUPER")) {
        let (?after = strafter(?rankName, "SUPER")) {
            return(concat("Super", substr(?after, 1, 1), lcase(substr(?after, 2))))
        }
    }
    else if (strstarts(?rankName, "INFRA")) {
        let (?after = strafter(?rankName, "INFRA")) {
            return(concat("Infra", substr(?after, 1, 1), lcase(substr(?after, 2))))
        }
    }
    else if (strstarts(?rankName, "PARV")) {
        let (?after = strafter(?rankName, "PARV")) {
            return(concat("Parv", substr(?after, 1, 1), lcase(substr(?after, 2))))
        }
    }
    else { return(concat(substr(?rankName, 1, 1), lcase(substr(?rankName, 2)))) }
}

# Format author name removing extra spaces at beginning and end of the name and after a dot
# (like in TAXREF)
# " (Bloch, 1782)" => "(Bloch, 1782)";
# "A. Gray" => "A.Gray"
function sms:formatAuthorName(?author) {
            replace( 
                replace(?author, "(\\. )", "."), 
                "(^ *)|( *$)",
                "" )
}
