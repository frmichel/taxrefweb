PREFIX dct:     <http://purl.org/dc/terms/>.
PREFIX dsw:     <http://purl.org/dsw/>.
PREFIX dwc:     <http://rs.tdwg.org/dwc/terms/>.
PREFIX dwciri:  <http://rs.tdwg.org/dwc/iri/>.
PREFIX schema: <http://schema.org/>.

# See the Darwin-SW model at https://github.com/darwin-sw/dsw
CONSTRUCT {

    ?occurrenceUri a        dwc:Occurrence;
        dct:identifier      ?occurrenceKeyStr;
        schema:url          ?page;

        dsw:atEvent [
            a               dwc:Event;
            dwc:recordedBy  ?rightsHolder;
            dwc:eventDate   ?date;
            dsw:locatedAt [
                a                       dct:Location;
                dwc:country             ?country;
                dwc:countryCode         ?countryCode;
                dwc:decimalLongitude    ?decimalLongitude;
                dwc:decimalLatitude     ?decimalLatitude;
            ];
        ];

        dsw:occurrenceOf [
            a dwc:Organism;
            dsw:hasIdentification [ dwciri:toTaxon ?taxonUri ];
        ].

    ?taxonUri
        a dwc:Taxon;
        schema:name         {name};    # RÃ©inject the input parameter
        dwc:taxonID         ?acceptedTaxonId;
        dwc:acceptedName    ?acceptedName.
}
WHERE {
    ?result
        api:key                 ?key;

        api:taxonKey            ?taxonKey;
        api:acceptedTaxonKey    ?acceptedTaxonKey;
	    api:scientificName      ?scientificName;
        api:acceptedScientificName ?acceptedScientificName;
        api:taxonomicStatus     ?taxonomicStatus;

        api:decimalLongitude    ?decimalLongitude;
        api:decimalLatitude     ?decimalLatitude;
        api:country             ?country;
        api:countryCode         ?countryCode;
        .

    OPTIONAL { ?result api:rightsHolder     ?rightsHolder }
    OPTIONAL { ?result api:license          ?license }

    OPTIONAL { ?result api:modified         ?modified }
    OPTIONAL { ?result api:dateIdentified   ?dateIdentified }
    OPTIONAL { ?result api:lastInterpreted  ?lastInterpreted }

    bind (IF(?taxonomicStatus = "ACCEPTED", ?scientificName, ?acceptedScientificName) AS ?acceptedName)
    bind (str(IF(?taxonomicStatus = "ACCEPTED", ?taxonKey, ?acceptedTaxonKey)) AS ?acceptedTaxonId)

    bind (IRI(concat("http://example.org/ld/gbif/taxon/", ?acceptedTaxonId)) AS ?taxonUri)
    bind (str(?key) AS ?occurrenceKeyStr)
    bind (IRI(concat("http://example.org/ld/gbif/occurrence/", ?key)) AS ?occurrenceUri)
    bind (IRI(concat("https://www.gbif.org/occurrence/", ?key)) AS ?page)
    bind (xsd:dateTime(coalesce(?dateIdentified, ?modified, ?lastInterpreted)) as ?date)
}
