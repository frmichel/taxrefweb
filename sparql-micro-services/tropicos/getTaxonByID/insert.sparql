INSERT {
    ?taxonUri a dwc:Taxon;

        dwc:scientificNameAuthorship ?authorname;
        dwc:scientificName ?scientificname;
        taxrefp:hasRank ?rankUri;
        dwc:genus ?genus_name;
        dwc:family ?family_name;
        schema:sameAs ?page.
}
WHERE {
    ?c
        api:NameId ?id;
        api:Author ?author;
        api:ScientificName ?scientificname;
	    api:ScientificNameWithAuthors ?full_reference_name;
        api:Rank ?rankName.
	OPTIONAL { ?c api:Genus ?genus_name}.
    OPTIONAL { ?c api:Family ?family_name}.
    OPTIONAL { ?c api:Source ?url}.
    OPTIONAL { ?c api:BasionymAuthor ?basAuthor}.

    #adding basionym name inside round brackets at the beginning of the authorname (as should be by convention in botany)
    bind (concat("(", ?basAuthor, ") ", ?author) AS ?fullauthor)
    bind (sms:formatAuthorName(coalesce(?fullauthor, ?author)) AS ?authorname)
    bind (IRI(?url) AS ?page)
	bind (sms:getTaxrefRankUri(?rankName) AS ?rankUri)
	bind (IRI(concat("http://example.org/ld/tropicos/taxon/", ?id)) AS ?taxonUri)
	
}

function sms:getTaxrefRankUri(?rankName){
	IRI(concat("http://taxref.mnhn.fr/lod/taxrank/", sms:getTaxrefRankName(?rankName)))
}

#Return the TAXREF-LD rank name given the rank name in Tropicos
function sms:getTaxrefRankName(?rankName){
	LET(?name = concat(UCASE(SUBSTR(?rankName,1,1)), SUBSTR(?rankName,2))){
        if(regex(?name, "Sub")){
                return(replace(?name, "Sub", "Sub-"))
        }else if(regex(?name, "Super")){
                return(replace(?name, "Super", "Super-"))
        }else if(regex(?name, "Infra")){
                return(replace(?name, "Infra", "Infra-"))
        }else if(regex(?name, "Parv")){
                return(replace(?name, "Parv", "Parv-"))
        }else{
                return(?name)
        }
    }
}

# Format author name removing extra whitespaces at beginning and end of the name and after a dot
# " (Bloch, 1782)" => "(Bloch, 1782)";
# "A. Gray" => "A.Gray"
function sms:formatAuthorName(?author){
    replace(replace(?author, "(\\. )", "."), "(^ *)|( *$)", "")
}