CONSTRUCT {
    ?taxonUri a dwc:Taxon;

        dwc:scientificNameID         ?id;
        dwc:scientificNameAuthorship ?authorname;
        dwc:scientificName           ?scientificname;
        taxrefp:hasRank              ?rankUri;
        dwc:family                   ?family_name;
        dwc:namePublishedIn          ?publishedIn;
        schema:sameAs                ?page;
        .
}
WHERE {
    ?c
        api:NameId                      ?id;
        api:Author                      ?author;
        api:ScientificName              ?scientificname;
        api:RankAbbreviation            ?rankAbbr.

    OPTIONAL { ?c api:Family         ?family_name }
    OPTIONAL { ?c api:DisplayDate         ?date }
    OPTIONAL { ?c api:DisplayReference     ?publishedIn }

    bind(coalesce(concat(?author, ", ", ?date), ?author) AS ?authorname)
    bind (sms:getRankUriByAbbr(?rankAbbr) AS ?rankUri)
    bind (IRI(concat("https://www.tropicos.org/Name/", ?id)) AS ?page)
    bind (IRI(concat("http://example.org/ld/tropicos/taxon/", ?id)) AS ?taxonUri)
}

function sms:getRankUriByAbbr(?rankAb) {
    IRI(concat("http://taxref.mnhn.fr/lod/taxrank/", sms:getTaxrefRankName(?rankAb)))
}

# Return the TAXREF-LD rank name given the rank abbreviation
function sms:getTaxrefRankName(?rankAb) {
    if      (?rankAb = "regn.")    { return("Kingdom") }
    else if (?rankAb = "subregn.") { return("SubKingdom") }
    else if (?rankAb = "phyl.")    { return("Phylum") }
    else if (?rankAb = "subphyl.") { return("SubPhylum") }
    else if (?rankAb = "class.")   { return("Class") }
    else if (?rankAb = "subclass."){ return("SubClass") }
    else if (?rankAb = "ord.")     { return("Order") }
    else if (?rankAb = "subord.")  { return("SubOrder") }
    else if (?rankAb = "fam.")     { return("Family") }
    else if (?rankAb = "gen.")     { return("Genus") }
    else if (?rankAb = "sp.")      { return("Species") }
    else if (?rankAb = "subsp.")   { return("SubSpecies") }
    else if (?rankAb = "f.")       { return("Forma") }
    else if (?rankAb = "f.sp.")    { return("FormaSpecies") }
    else if (?rankAb = "trib.")    { return("Tribe") }
    # '*' or a single greek or latin letter represents variety
    else if (?rankAb = "var." || ?rankAb = "*" || (STRLEN(?rankAb) = 1 && REGEX(?rankAb, "([α-ω])|([A-Ω])|([a-z])|([A-Z])")))
                                   { return("Varietas") }
}

# Format author name removing extra whitespaces at beginning and end of the name and after a dot
# " (Bloch, 1782)" => "(Bloch, 1782)";
# "A. Gray" => "A.Gray"
#function sms:formatAuthorName(?author) {
#    replace(replace(?author, "(\\. )", "."), "(^ *)|( *$)", "")
#}