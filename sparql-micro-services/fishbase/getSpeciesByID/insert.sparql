INSERT {
    ?taxonUri a dwc:Taxon;

        dwc:scientificNameAuthorship ?authorname;
        dwc:scientificName ?name;
        dwc:namePublishedInYear ?year;
        dwc:vernacularName ?vernacularName;
        taxrefp:hasRank ?rankUri;
        taxrefp:habitat ?habitats;
.
}
WHERE {
    ?content
        api:SpecCode ?specCode;
        api:SynCode ?id;
        api:SynGenus ?genus_name;
        api:SynSpecies ?species;
        api:Author ?author;
        api:TaxonLevel ?rankName;
        api:Status ?status;
        api:Year ?year;

	# the scientific name of the taxon is the concatenation of genus and species name
	bind(concat(?genus_name, " ", ?species) AS ?name)
	bind(IRI(concat("http://example.org/sparql-ms-tw/fishbase/getSpeciesInfoByID?id=", ?specCode)) AS ?service)
	OPTIONAL {
        SERVICE ?service {
            [] taxrefp:habitat ?habitats;
                dwc:vernacularName ?vernacularName;
        }
	}
    bind (sms:getRankUri(?rankName) AS ?rankUri)
    bind (sms:formatAuthorName(?author) AS ?authorname)
    bind (IRI(concat("http://example.org/ld/fishbase/taxon/", ?id)) AS ?taxonUri)
}


function sms:getRankUri(?rankName){
	IRI(concat("http://taxref.mnhn.fr/lod/taxrank/", sms:getTaxrefRankName(?rankName)))
}

#Return the TAXREF-LD rank name, given the name rank name in FishBase
function sms:getTaxrefRankName(?rkName){
	 if(?rkName = "Forma"){
		return("Form")
	}else if(?rkName = "Subforma"){
		return("Sub-form")
	}else if(regex(?rkName, "Sub")){
		return(replace(?rkName, "Sub", "Sub-"))
	}else if(regex(?rkName, "Super")){
		return(replace(?rkName, "Super", "Super-"))
	}else if(regex(?rkName, "Infra")){
		return(replace(?rkName, "Infra", "Infra-"))
	}else if(regex(?rkName, "Parv")){
		return(replace(?rkName, "Parv", "Parv-"))
	}else{
		return(?rkName)
	}
}

# Format author name removing extra spaces at beginning and end of the name and after a dot
# " (Bloch, 1782)" => "(Bloch, 1782)";
# "A. Gray" => "A.Gray"
function sms:formatAuthorName(?author){
    replace(replace(?author, "(\\. )", "."), "(^ *)|( *$)", "")
}