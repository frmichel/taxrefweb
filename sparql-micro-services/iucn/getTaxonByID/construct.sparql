PREFIX taxrefp: <http://taxref.mnhn.fr/lod/property/>.

CONSTRUCT {
    ?taxonUri a dwc:Taxon;

        dwc:scientificName           ?scientificName;
        dwc:scientificNameID         ?id;
        dwc:scientificNameAuthorship ?authorshipName;
        dwc:taxonomicStatus          ?status;

        dwc:kingdom                  ?kingdom;
        dwc:phylum                   ?phylum;
        dwc:class                    ?class;
        dwc:order                    ?order;
        dwc:family                   ?family;
        dwc:genus                    ?genus;

        schema:sameAs                ?page;
        #skos:broader                 ?parentUri;
		.
}
WHERE {
    ?results                api:result ?result.
    ?result
        api:taxonid          ?id;
        api:authority      ?authorship;
        api:scientific_name        ?scientificName.

    OPTIONAL { ?result api:name_status     ?status }
    OPTIONAL { ?result api:kingdom         ?kingdom }
    OPTIONAL { ?result api:phylum          ?phylum }
    OPTIONAL { ?result api:class           ?class }
    OPTIONAL { ?result api:order           ?order }
    OPTIONAL { ?result api:family          ?family }
    OPTIONAL { ?result api:genus           ?genus}

    bind (IRI(concat("http://example.org/ld/col/taxon/", ?id)) AS ?taxonUri)
    bind (IRI(concat("http://apiv3.iucnredlist.org/api/v3/taxonredirect/", ?id)) AS ?page)
    
    bind (sms:formatAuthorName(?authorship) AS ?authorshipName)

}

# Format author name removing extra spaces at beginning and end of the name and after a dot
# and moving basionymName (in round brackets at the end of the name) to the beginning of the name (like in TAXREF)
# " (Bloch, 1782)" => "(Bloch, 1782)";
# "A. Gray" => "A.Gray"
# "E.Mey., 1839 (L.)" => "(L.) E.Mey., 1839"
function sms:formatAuthorName(?author) {
    LET (?basionym = IF(regex(?author, "\\(.*\\)$"), replace(?author, "^.*(\\(.*\\))$", "$1"), "")) {
        LET (?author = concat(?basionym , " ", replace(?author, "\\(.*\\)$", ""))) {
            replace(replace(?author, "(\\. )", "."), "(^ *)|( *$)", "")
        }
    }
}
